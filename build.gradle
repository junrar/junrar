plugins {
    id 'java-library'
    id 'maven-publish'
    id 'checkstyle'
    id 'jacoco'
    id 'com.github.ben-manes.versions' version '0.28.0'
    id 'com.autonomousapps.dependency-analysis' version '0.52.0'
    id 'dev.jacomet.logging-capabilities' version '0.9.0'
    id 'com.jfrog.bintray' version '1.8.5'
}

repositories {
    jcenter()
}

dependencies {
    implementation 'org.slf4j:slf4j-api:1.7.9'

    testRuntimeOnly 'ch.qos.logback:logback-classic:1.2.3'
    testImplementation 'org.junit.jupiter:junit-jupiter:5.6.2'
    testImplementation 'commons-io:commons-io:1.3.2'
    testImplementation 'org.mockito:mockito-core:2.23.4'
    testImplementation 'org.assertj:assertj-core:3.16.1'
}

group = 'com.github.junrar'
description = 'rar decompression library in plain java'


sourceCompatibility = '1.8'
targetCompatibility = '1.8'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        maven(MavenPublication) {
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'Java Unrar'
                description = "${description}"
                url = 'https://github.com/junrar/junrar'
                licenses {
                    license {
                        name = 'UnRar License'
                        url = 'https://github.com/junrar/junrar/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'gotson'
                        name = 'Gauthier Roebroeck'
                    }
                }
                scm {
                    url = 'https://github.com/junrar/junrar.git'
                }
            }
        }
    }
    repositories {
        mavenLocal()
    }
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.wrapper {
    gradleVersion = '6.5.1'
    distributionType = Wrapper.DistributionType.ALL
}

test {
  useJUnitPlatform()
}

checkstyle {
    configFile = file("${rootDir}/checkstyle.xml")
    toolVersion = '8.29'
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['maven']
    publish = true
//    override = true
    pkg {
        repo = 'junrar'
        name = "${project.group}:${project.name}"
//        licenses = ['Unrar']
        vcsUrl = 'https://github.com/junrar/junrar.git'
        version {
            name = "${project.version}"
            desc = "${project.description}"
            released  = new Date()
            vcsTag = "v${project.version}"
            gpg {
                sign = true
                passphrase = System.getenv('GPG_PASSPHRASE')
            }
            mavenCentralSync {
                sync = true
            }
        }
    }
}

jacocoTestReport {
    reports {
        xml.enabled true
        html.enabled false
    }
}